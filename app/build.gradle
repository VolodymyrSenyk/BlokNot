apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdkVersion versions.application.compileSdk
    buildToolsVersion versions.application.buildTools

    defaultConfig {
        applicationId "com.senyk.volodymyr.bloknot"
        minSdkVersion versions.application.minSdk
        targetSdkVersion versions.application.targetSdk
        versionName "$versions.application.versionName (${getVersion()})"
        versionCode versions.application.versionCode

        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file(getKeystoreProperty('releaseStoreFile'))
            storePassword getKeystoreProperty('releaseStorePassword')
            keyAlias getKeystoreProperty('releaseKeyAlias')
            keyPassword getKeystoreProperty('releaseKeyPassword')
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        experimental = true
    }

    buildFeatures {
        dataBinding = true
    }
}

String getKeystoreProperty(String propertyName) {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    }
    if (keystoreProperties.containsKey(propertyName)) {
        return keystoreProperties[propertyName]
    } else if (System.getenv(propertyName) != null) {
        return System.getenv(propertyName)
    } else {
        throw new IllegalStateException("Keystore property not found: $propertyName")
    }
}

dependencies {
    // multidex
    implementation "androidx.multidex:multidex:$versions.multidex"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin.language"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin.language"
    implementation "androidx.core:core-ktx:$versions.kotlin.ktx"

    // view
    implementation "androidx.appcompat:appcompat:$versions.view.appcompat"
    implementation "androidx.fragment:fragment-ktx:$versions.view.ktx_fragment"
    implementation "androidx.constraintlayout:constraintlayout:$versions.view.constraint"
    implementation "com.google.android.material:material:$versions.view.material"

    // di
    implementation "com.google.dagger:dagger:$versions.di.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.di.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.di.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.di.dagger"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation.arch_component"
    implementation "androidx.navigation:navigation-ui-ktx:$versions.navigation.arch_component"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
    kapt "androidx.lifecycle:lifecycle-common-java8:$versions.lifecycle"

    // persistence
    implementation "androidx.room:room-runtime:$versions.persistence.room"
    implementation "androidx.room:room-ktx:$versions.persistence.room"
    implementation "androidx.room:room-rxjava2:$versions.persistence.room"
    kapt "androidx.room:room-compiler:$versions.persistence.room"

    // rx
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rx.android"
    implementation "io.reactivex.rxjava2:rxjava:$versions.rx.java"
    implementation "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin"
    implementation "com.vanniktech:rxpermission:$versions.rx.permissions"

    // frameworks
    implementation "com.google.code.gson:gson:$versions.frameworks.gson"
}
